plugins {
    id "com.jfrog.bintray" version "1.1"
}

apply plugin: "scala"
apply plugin: "maven-publish"
sourceCompatibility = 1.7
version = "4.0.0-SNAPSHOT"
def scalaVersion = "2.11.6"

def bintrayVersion = {
    if (version.endsWith('-SNAPSHOT')) {
        java.text.SimpleDateFormat format = new java.text.SimpleDateFormat('yyyyMMddHHmm')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
        return version.replace('SNAPSHOT', format.format(new Date()))
    } else {
        return version
    }
}()

task wrapper(type: Wrapper) {
    gradleVersion = "2.3"
}

repositories {
    mavenCentral()
}

jar {
    baseName = "orbroker_2.11"
    manifest {
        attributes "Implementation-Title": "orbroker", "Implementation-Version": version
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allScala
}
task docsJar(type: Jar, dependsOn: scaladoc) {
    classifier "docs"
    from scaladoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'org.orbroker'
            artifactId jar.baseName
            version bintrayVersion

            from components.java
            artifact sourceJar
            artifact docsJar
            pom.withXml { xml ->
                xml.asNode().dependencies.dependency.each { dep ->
                    dep.scope[0].value = "provided"
                }
            }
        }
    }
}

bintray {
    user = System.properties.bintrayUser ?: "n/a"
    key = System.properties.bintrayApiKey ?: "n/a"
    dryRun = (user == "n/a" || key == "n/a")
    publish = !dryRun
    publications = ['maven']
    pkg {
        repo = "maven"
        name = "ORBroker"
        version {
            name = bintrayVersion
        }
    }
}

dependencies {
    testCompile "junit:junit:4.11"

    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.freemarker:freemarker:2.3.22"
    compile "joda-time:joda-time:2.7"
    compile "org.apache.velocity:velocity:1.7"
    compile "org.apache.derby:derby:10.11.1.1"
}

compileScala {
    scalaCompileOptions.additionalParameters = [
        "-feature", "-optimise","–unchecked", "-deprecation","–explaintypes",
        "–Xdisable-assertions", //"-Xfatal-warnings",
        "-Ybackend:GenBCode", "-Yclosure-elim",
    ]
}

compileTestScala {
    scalaCompileOptions.additionalParameters = [
        "-nowarn",
        "-Xcheckinit", "-optimise",
        "-Ybackend:GenBCode", "-Yclosure-elim",
    ]
}

scaladoc {
    def javaLib = new java.io.File("${System.properties['java.home']}/lib/rt.jar")
    def scalaLib = classpath.find {
        it.name.startsWith("scala-library")
    }
    def extDocParm = "-doc-external-doc:$scalaLib#www.scala-lang.org/api/$scalaVersion/,$javaLib#docs.oracle.com/javase/7/docs/api/"
    scalaDocOptions.additionalParameters = [extDocParm,]
}
